buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'spoon'
apply plugin: 'jacoco'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

ext {
    jacocoVersion = "0.7.5.201505241946"
}

configurations {
    all {
        resolutionStrategy {
            force "org.jacoco:org.jacoco.agent:$jacocoVersion"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin https://kotlinlang.org
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // AndroidSupportLibrary https://developer.android.com/topic/libraries/support-library/features.html
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'

    // JUnit http://junit.org/junit4/
    testCompile 'junit:junit:4.12'

    // Espresso https://developer.android.com/training/testing/ui-testing/espresso-testing.html
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Spoon https://github.com/square/spoon
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'

    // Jacoco http://www.eclemma.org/jacoco/
    // A specific version is needed for debug testCoverageEnabled
    // working example http://stackoverflow.com/questions/41960873/error-after-updating-android-studio-to-2-2-3
    androidTestCompile "org.jacoco:org.jacoco.agent:$jacocoVersion"
}

sonarqube {
    properties {
        property "sonar.projectName", "HelloMultiProjectAndroid-SDK-api"
        property "sonar.projectKey", "HelloMultiProjectAndroid-SDK-api"
    }
}

repositories {
    jcenter()
    mavenCentral()
}

spoon {
    debug = true
    codeCoverage = true
    grantAllPermissions = true
    adbTimeout = 60
}

// Do not do this because the coverage report can not generate directories that do not yet exist.
//tasks.spoon.dependsOn 'createDebugCoverageReport'
